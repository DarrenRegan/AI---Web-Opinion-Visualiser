FUNCTION_BLOCK score // Block definition

//https://learnonline.gmit.ie/pluginfile.php/154765/mod_resource/content/1/iec_1131_7_cd1.pdf
//http://jfuzzylogic.sourceforge.net/html/manual.html
// Define input variables
VAR_INPUT
		title : REAL;
		heading : REAL;
		body : REAL;
END_VAR

// Define output variable
VAR_OUTPUT
		score : REAL;
END_VAR

FUZZIFY title // Fuzzify input variable 'title': {'significant', 'insignificant'}
		TERM significant := (50, 0) (200, 1);
		TERM insignificant := (0, 1) (40, 0);
END_FUZZIFY

FUZZIFY heading // Fuzzify input variable 'heading': {'relevant', 'irrelevant'}
		TERM relevant := (20, 0) (100, 1);
		TERM irrelevant := (0, 1) (10, 0);
END_FUZZIFY

FUZZIFY body // Fuzzify input variable 'body': {'barely', 'average', 'frequent'}
		TERM barely := (0, 1) (70, 0);
		TERM average :=  (60, 0) (70, 1) (100, 0);
		TERM frequent :=  (90, 0) (500, 1);
END_FUZZIFY

DEFUZZIFY score // Defzzzify output variable 'score' : {'low', 'average', 'high'}
		TERM low := 10;
		TERM average := 50;
		TERM high := 100;
		METHOD : COGS; // Use 'Center Of Gravity' defuzzification method
		DEFAULT := 0; // Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

// Inference rules
RULEBLOCK No1
		AND : MIN; // Use 'min' for 'and'
		ACT : MIN; // Use 'min' activation method
		ACCU : MAX; // Use 'max' accumulation method
		//High Scoring
		RULE 1 :    IF title IS significant
				AND heading IS relevant
				OR body IS frequent 
				THEN score IS high;
		//AVG Scoring
		RULE 2 :	IF title IS significant 
				AND heading IS relevant
				OR body IS average
				THEN score IS average;
		//AVG Scoring
		RULE 3 :	IF title IS significant
				AND heading IS relevant 
				OR body IS barely 
				THEN score IS average;
		//AVG Scoring
		RULE 4 :	IF title IS significant 
				OR heading IS irrelevant 
				AND body IS average 
				THEN score IS average;
		//Low Scoring
		RULE 5 :	IF title IS significant
				AND heading IS irrelevant
				AND body IS barely 
				THEN score IS low;
		//AVG Scoring
		RULE 6 :	IF title IS significant 
				AND heading IS irrelevant 
				AND body IS frequent 
				THEN score IS average;
		//Low Scoring
		RULE 7 :	IF title IS insignificant 
				OR heading IS irrelevant 
				OR body IS barely 
				THEN score IS low;
		//Low Scoring
		RULE 8 :	IF title IS insignificant 
				OR heading IS irrelevant 
				AND body IS average 
				THEN score IS low;
		//AVG Scoring
		RULE 9 :	IF title IS insignificant 
				OR heading IS irrelevant 
				AND body IS frequent 
				THEN score IS average;
		//Low Scoring
		RULE 10 :	IF title IS insignificant 
				AND heading IS relevant 
				AND body IS average 
				THEN score IS low;
		//Low Scoring
		RULE 11 :	IF title IS insignificant 
				AND heading IS relevant 
				AND body IS barely 
				THEN score IS low;
		//AVG Scoring
		RULE 12 :	IF title IS insignificant 
				AND heading IS relevant 
				AND body IS frequent 
				THEN score IS average;
END_RULEBLOCK

END_FUNCTION_BLOCK